buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

// Load SDK_REGISTRY_TOKEN from local.properties for Mapbox
def localPropertiesFile = rootProject.file('local.properties')
def sdkRegistryToken = ""
if (localPropertiesFile.exists()) {
    def props = new Properties()
    localPropertiesFile.withInputStream { props.load(it) }
    sdkRegistryToken = props.getProperty("SDK_REGISTRY_TOKEN", System.getenv("SDK_REGISTRY_TOKEN") ?: "")
}

allprojects {
    repositories {
        google()
        mavenCentral()
        // Flutter engine artifacts - required for all subprojects
        maven {
            url 'https://storage.googleapis.com/download.flutter.io'
        }
        // Mapbox SDK
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = sdkRegistryToken
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

// Ensure Flutter storage repository is added to all subprojects after evaluation
subprojects {
    afterEvaluate { project ->
        project.repositories {
            maven {
                url = uri('https://storage.googleapis.com/download.flutter.io')
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
