pluginManagement {
    def localProperties = new Properties()
    file("local.properties").withInputStream { localProperties.load(it) }

    def flutterSdkPath = localProperties.getProperty("flutter.sdk")
    assert flutterSdkPath != null, "flutter.sdk not set in local.properties"

    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.6.0" apply false
    id "org.jetbrains.kotlin.android" version "2.1.0" apply false
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)

    // Load SDK_REGISTRY_TOKEN from local.properties for Mapbox
    def localPropertiesFile = file("local.properties")
    def sdkRegistryToken = ""
    if (localPropertiesFile.exists()) {
        def props = new Properties()
        localPropertiesFile.withInputStream { props.load(it) }
        sdkRegistryToken = props.getProperty("SDK_REGISTRY_TOKEN", System.getenv("SDK_REGISTRY_TOKEN") ?: "")
    }

    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = sdkRegistryToken
            }
        }
    }
}

include ":app"
